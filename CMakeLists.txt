PROJECT ( llvm_translator )
CMAKE_MINIMUM_REQUIRED ( VERSION 2.8.8 FATAL_ERROR )

SET ( LIBRARY_NAME "llvm_translator" )
SET ( DEFAULT_BUILD_TYPE "Debug" )
SET ( CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${PROJECT_SOURCE_DIR}/cmake" )

FIND_PACKAGE ( LLVM 3.4 REQUIRED )
FIND_PACKAGE ( QEMU )
FIND_PACKAGE ( GLIB REQUIRED )
FIND_PACKAGE ( Curses REQUIRED )

INCLUDE ( cmake/LLVMTools.cmake )


INCLUDE_DIRECTORIES ( include ${QEMU_INCLUDE_DIRECTORIES} ${LLVM_INCLUDE_DIRS} ${GLIB_INCLUDE_DIRS} )
ADD_DEFINITIONS ( -DNEED_CPU_H -D__STDC_CONSTANT_MACROS -D__STDC_LIMIT_MACROS -DCONFIG_LLVM )
SET ( CMAKE_SHARED_LINKER_FLAGS "${CMAKE_SHARED_LINKER_FLAGS} -Wl,-undefined,dynamic_lookup" )

SET ( LLVM_TRANSLATOR_SRC 
	src/tcg-llvm.cpp
	src/tcg-plugin-main.cpp)
    
    
FOREACH ( QEMU_TARGET ${QEMU_TARGETS} )
    STRING ( REPLACE "," ";" QEMU_TARGET_LIST ${QEMU_TARGET} )
    LIST ( GET QEMU_TARGET_LIST 0 QEMU_TARGET_BASE_ARCH )
    LIST ( GET QEMU_TARGET_LIST 1 QEMU_TARGET_DIRECTORY )
    LIST ( GET QEMU_TARGET_LIST 2 QEMU_TARGET_ARCH )
    
    # Build the TCG plugin
	SET ( CURRENT_LIB_NAME ${LIBRARY_NAME}-${QEMU_TARGET_DIRECTORY} )

	ADD_LIBRARY ( ${CURRENT_LIB_NAME} SHARED ${LLVM_TRANSLATOR_SRC} )
	TARGET_LINK_LIBRARIES ( ${CURRENT_LIB_NAME} ${LLVM_LIBRARIES} ${CURSES_LIBRARY} )
	SET_PROPERTY ( TARGET ${CURRENT_LIB_NAME} APPEND PROPERTY INCLUDE_DIRECTORIES 
		"${QEMU_SOURCE}/target-${QEMU_TARGET_BASE_ARCH}" "${QEMU_BUILD}/${QEMU_TARGET_DIRECTORY}" )
	SET_PROPERTY ( TARGET ${CURRENT_LIB_NAME} APPEND PROPERTY COMPILE_FLAGS 
		"-DTARGET_EMULATION_MODE=\\\"${QEMU_TARGET_DIRECTORY}\\\" -DTARGET_NAME=\\\"${QEMU_TARGET_ARCH}\\\"" ) 
    
    # Build the LLVM helper library
    SET ( OP_HELPER_LIB_NAME op_helper-${QEMU_TARGET_DIRECTORY} )
    SET ( OP_HELPER_SOURCE ${QEMU_SOURCE}/target-${QEMU_TARGET_BASE_ARCH}/op_helper.c )
    
    #If the op_helper.c file does not exist (currently for i386) we need to generate it
    #TODO: It would be nicer to have a target for that
    IF ( NOT EXISTS ${OP_HELPER_SOURCE} )
        SET ( OP_HELPER_SOURCE ${CMAKE_BINARY_DIR}/CMakeFiles/${OP_HELPER_LIB_NAME}.dir/op_helper.c )
        EXECUTE_PROCESS ( 
            COMMAND ${CMAKE_SOURCE_DIR}/scripts/generate_helper.sh 
            OUTPUT_FILE ${OP_HELPER_SOURCE} 
            WORKING_DIRECTORY ${QEMU_SOURCE}/target-${QEMU_TARGET_BASE_ARCH} )
    ENDIF ()
    SET_PROPERTY ( SOURCE ${OP_HELPER_SOURCE} APPEND PROPERTY INCLUDE_DIRECTORIES
        "${QEMU_SOURCE}/target-${QEMU_TARGET_BASE_ARCH}" "${QEMU_BUILD}/${QEMU_TARGET_DIRECTORY}" )
    ADD_BITCODE ( ${OP_HELPER_LIB_NAME} ${OP_HELPER_SOURCE} )
    SET_PROPERTY ( TARGET ${OP_HELPER_LIB_NAME} APPEND PROPERTY INCLUDE_DIRECTORIES 
        "${QEMU_SOURCE}/target-${QEMU_TARGET_BASE_ARCH}" "${QEMU_BUILD}/${QEMU_TARGET_DIRECTORY}" )
        
ENDFOREACH ()